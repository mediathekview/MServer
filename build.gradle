import java.nio.file.Files
import java.nio.file.Paths
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.jk1.dependency-license-report" version "0.3.5"
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven'
apply from: "${project.rootDir}/gradle/eclipse.gradle"


sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'de.mediathekview'
version = '3.1.1'

def jarName = 'MServer.jar'
def mainClass = 'mServer.Main'

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:all']
}
compileTestJava {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:all']
}

task copyRuntimeLibs(type: Copy) {
    into "libs"
    from configurations.testRuntime - configurations.runtime
}

repositories {
    mavenLocal()
    maven {
        url "https://repo.mediathekview.de/repository/maven-public/"
    }
}

sourceSets {
    developTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/developTest/java')
        }
        resources.srcDir file('src/test/developTest/resources')
    }
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/integrationTest/java')
        }
        resources.srcDir file('src/test/integrationTest/resources')
    }
    performanceTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test/performanceTest/java')
        }
        resources.srcDir file('src/test/performanceTest/resources')
    }
}

configurations {
    developTestCompile.extendsFrom testCompile
    developTestRuntime.extendsFrom testRuntime
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    performanceTestCompile.extendsFrom testCompile
    performanceTestRuntime.extendsFrom testRuntime
}

test {
}

task developTest(type: Test) {
    testClassesDir = sourceSets.developTest.output.classesDir
    classpath = sourceSets.developTest.runtimeClasspath
    outputs.upToDateWhen { false }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

task performanceTest(type: Test) {
    logger.info("hier")
    testClassesDir = sourceSets.performanceTest.output.classesDir
    classpath = sourceSets.performanceTest.runtimeClasspath
    outputs.upToDateWhen { false }
    
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    compile 'de.mediathekview:MLib:3.0.1'
    compile 'commons-net:commons-net:3.5'
    compile 'org.apache.commons:commons-compress:1.12'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.tukaani:xz:1.5'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.sun.jersey:jersey-client:1.8'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'


    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'fm.void.jetm', name: 'jetm', version: '1.2.3'

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.squareup.okhttp3:mockwebserver:3.6.0"
    testCompile "org.hamcrest:hamcrest-all:1.3"
}

configurations.compile {
    resolutionStrategy.dependencySubstitution {
        def substituteIfExists = { String moduleName, String projectName ->
            if(findProject(projectName)) {
               assert !(project.hasProperty('org.gradle.configureondemand') && Boolean.valueOf(project.getProperty('org.gradle.configureondemand'))) : "org.gradle.configureondemand = true will cause problems when substituting a project"
               logger.lifecycle "$project: Substituting module '$moduleName' with project $projectName"
               substitute module(moduleName) with project(projectName)
            }
        }
        substituteIfExists('de.mediathekview:MLib', ':MLib')
    }
}

ext {
    propsFile = file('src/main/resources/version.properties').absoluteFile
    if (!propsFile.exists()) {
        Files.createFile(propsFile.toPath())
    }
    repoZugangFile = file('scripte/deploy/RepoZugang.properties').absoluteFile
    if (!repoZugangFile.exists()) {
        Files.createFile(repoZugangFile.toPath())
    }
}

def loadVersionProperties() {
    Properties props = new Properties()
    props.load(propsFile.newDataInputStream())
    return props
}

def loadRepoZugangProperties() {
    Properties props = new Properties()
    props.load(repoZugangFile.newDataInputStream())
    return props
}

uploadArchives {
    repositories.mavenDeployer {
        Properties props = loadRepoZugangProperties()
        def nexusUser = props.getProperty('repoUser')
        def nexusPw = props.getProperty('repoPw')
        repository(url: "https://repo.mediathekview.de/repository/maven-releases/") {
            authentication(userName:  nexusUser, password:  nexusPw)
        }
        snapshotRepository(url: "https://repo.mediathekview.de/repository/maven-snapshots/") {
            authentication(userName:  nexusUser, password:  nexusPw)
        }
    }
}
build.dependsOn(install)

task updateVersion {
    doLast {
        Properties props = loadVersionProperties()
        def oldVersion = props.getProperty('VERSION')
        String buildDate = new Date().format('dd.MM.yyyy HH:mm:ss')
        if (!oldVersion.equals(project.version)) {
            logger.lifecycle "==mserver======================"
            logger.lifecycle "Version: $project.version"
            logger.lifecycle "Baudatum: $buildDate"
            logger.lifecycle "==mserver======================"
            props.setProperty('VERSION', project.version)
            props.setProperty('DATE', buildDate)
            props.store(propsFile.newWriter(), null)
        }
    }
}

processResources.dependsOn updateVersion

licenseReport {
    outputDir = "build/Copyright/"
    excludes = [':MSearch', 'MediathekView:MLib']
    configurations = ['compile']
}

processResources.dependsOn generateLicenseReport

[distZip, distTar]*.shouldRunAfter compileJava, updateVersion, jar
distTar.compression = Compression.GZIP
distTar.extension = 'tar.gz'

jar {
    manifest {
        attributes(
                'Main-Class': mainClass,
                'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' ')
        )
    }
    archiveName = jarName
}

distributions {
    main {
        baseName = 'MServer'
        contents {
            into('lib') {
                from configurations.compile
            }
            from('dist') {
                filesMatching('**/*.sh') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
                filesMatching('**/*.txt') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
                filesMatching('**/*.command') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
            }
            from('build/libs') {
                include '*.jar'
            }
            from('build') {
                include 'Copyright/*'
            }
        }
    }
}

/**
 * This is how you pass arguments: "./gradlew run -Pargs=arg1,arg2,arg3
 */
task run(type: JavaExec, dependsOn: classes) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath

    if (project.hasProperty('args')) {
        args(project.args.split(','))
    }
}

/**
 * HOWTO debug:
 *  1. run "gradle debug"
 *  2. Call your IDE to connect to a remote java application on port 5005.
 *
 * This is how you pass arguments: "./gradlew debug -Pargs=arg1,arg2,arg3
 */
task debug(type: JavaExec, dependsOn: classes) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
    debug true

    if (project.hasProperty('args')) {
        args(project.args.split(','))
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if a new releass (tag) will be build!<b/>
 * Call this so: './gradlew build release -PnexusUser=[NEXUS_USER] -PnexusPw=[NEXUS_PASSWORD]'
 */
task release(dependsOn: 'uploadArchives') {
    doLast {
        println 'Released Version '+version
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if you push to develop!<b/>
 * Call this so: './gradlew build releaseSnapshot -PnexusUser=[NEXUS_USER] -PnexusPw=[NEXUS_PASSWORD]'
 */
task releaseSnapshot(dependsOn: 'uploadArchives') {
    doLast {
        println 'Released Snapshot Version '+version
    }
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(releaseSnapshot)) {
        version = version+'-SNAPSHOT'
    }
}

